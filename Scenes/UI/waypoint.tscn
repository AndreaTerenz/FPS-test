[gd_scene load_steps=2 format=2]

[sub_resource type="GDScript" id=1]
script/source = "extends Control

# Some margin to keep the marker away from the screen's corners.
const MARGIN = 8

enum MARKER_MODE {
	ALWAYS,
	NEVER,
	OFFSCREEN,
	ONSCREEN
}

onready var camera = get_viewport().get_camera()
onready var parent = get_parent()
onready var canvas = $CanvasLayer
onready var main_ui = get_node(\"%MainUI\")
onready var marker_ui = get_node(\"%MarkerUI\")

export(Color) var mod_color = Color.white
export(int) var layer = -1
export(MARKER_MODE) var marker_mode = MARKER_MODE.ALWAYS

export(NodePath) var main_content_ref
export(NodePath) var marker_content_ref

func _ready() -> void:
	if not parent is Spatial:
		push_error(\"The waypoint's parent node must inherit from Spatial.\")
	else:
		self.modulate = mod_color
		self.marker_ui.visible = (marker_mode != MARKER_MODE.NEVER)
		
		canvas.layer = layer
	
		get_content(main_content_ref, main_ui)
		get_content(marker_content_ref, marker_ui)
			
func get_content(ref: NodePath, dest: Control):
	var cont = Utils.try_get_node(ref, self) as Control
	if cont:
		Utils.transfer_node(cont, dest)
		cont.set_anchors_and_margins_preset(Control.PRESET_WIDE)

func _process(_delta):
	if not camera.current:
		# If the camera we have isn't the current one, get the current camera.
		camera = get_viewport().get_camera()
	var parent_translation = parent.global_translation
	var camera_transform = camera.global_transform
	var camera_translation = camera_transform.origin

	# We would use \"camera.is_position_behind(parent_translation)\", except
	# that it also accounts for the near clip plane, which we don't want.
	var is_behind = camera_transform.basis.z.dot(parent_translation - camera_translation) > 0

	# Fade the waypoint when the camera gets close.
	var distance = camera_translation.distance_to(parent_translation)
	modulate.a = clamp(range_lerp(distance, 0, 2, 0, 1), 0, 1 )

	var unprojected_position = camera.unproject_position(parent_translation)
	# `get_size_override()` will return a valid size only if the stretch mode is `2d`.
	# Otherwise, the viewport size is used directly.
	var viewport_base_size = (
			get_viewport().get_size_override() if get_viewport().get_size_override() > Vector2(0, 0)
			else get_viewport().size
	)

	var sticky = (marker_mode in [MARKER_MODE.ALWAYS, MARKER_MODE.OFFSCREEN])
	if not sticky:
		# For non-sticky waypoints, we don't need to clamp and calculate
		# the position if the waypoint goes off screen.
		canvas.offset = unprojected_position
		canvas.visible = not is_behind
		return

	# We need to handle the axes differently.
	# For the screen's X axis, the projected position is useful to us,
	# but we need to force it to the side if it's also behind.
	if is_behind:
		if unprojected_position.x < viewport_base_size.x / 2:
			unprojected_position.x = viewport_base_size.x - MARGIN
		else:
			unprojected_position.x = MARGIN

	# For the screen's Y axis, the projected position is NOT useful to us
	# because we don't want to indicate to the user that they need to look
	# up or down to see something behind them. Instead, here we approximate
	# the correct position using difference of the X axis Euler angles
	# (up/down rotation) and the ratio of that with the camera's FOV.
	# This will be slightly off from the theoretical \"ideal\" position.
	if is_behind or unprojected_position.x < MARGIN or \\
			unprojected_position.x > viewport_base_size.x - MARGIN:
		var look = camera_transform.looking_at(parent_translation, Vector3.UP)
		var diff = angle_diff(look.basis.get_euler().x, camera_transform.basis.get_euler().x)
		unprojected_position.y = viewport_base_size.y * (0.5 + (diff / deg2rad(camera.fov)))

	canvas.offset = Vector2(
			clamp(unprojected_position.x, MARGIN, viewport_base_size.x - MARGIN),
			clamp(unprojected_position.y, MARGIN, viewport_base_size.y - MARGIN)
	)

	marker_ui.visible = (marker_mode != MARKER_MODE.NEVER)
	main_ui.visible = true
	canvas.rotation = 0
	# Used to display a diagonal arrow when the waypoint is displayed in
	# one of the screen corners.
	var overflow = 0

	if canvas.offset.x <= MARGIN:
		# Left overflow.
		overflow = -PI/4
		main_ui.visible = false
		canvas.rotation = PI/2
	elif canvas.offset.x >= viewport_base_size.x - MARGIN:
		# Right overflow.
		overflow = PI/4
		main_ui.visible = false
		canvas.rotation = PI*3/2

	if canvas.offset.y <= MARGIN:
		# Top overflow.
		main_ui.visible = false
		canvas.rotation = PI + overflow
	elif canvas.offset.y >= viewport_base_size.y - MARGIN:
		# Bottom overflow.
		main_ui.visible = false
		canvas.rotation = -overflow
		
	if marker_mode == MARKER_MODE.OFFSCREEN:
		marker_ui.visible = not main_ui.visible

static func angle_diff(from, to):
	var diff = fmod(to - from, TAU)
	return fmod(2.0 * diff, TAU) - diff
"

[node name="Waypoint" type="Control"]
script = SubResource( 1 )

[node name="CanvasLayer" type="CanvasLayer" parent="."]
layer = -1

[node name="MainUI" type="Control" parent="CanvasLayer"]
unique_name_in_owner = true
margin_left = -61.0
margin_top = -52.0
margin_right = 65.0
margin_bottom = -16.0
__meta__ = {
"_edit_group_": true
}

[node name="MarkerUI" type="Control" parent="CanvasLayer"]
unique_name_in_owner = true
anchor_left = 0.5
anchor_top = 0.5
anchor_right = 0.5
anchor_bottom = 0.5
margin_left = -971.0
margin_top = -556.0
margin_right = -949.0
margin_bottom = -534.0
rect_pivot_offset = Vector2( 11, 11 )
__meta__ = {
"_edit_group_": true
}
